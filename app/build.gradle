apply plugin: 'com.android.application'

android {
    signingConfigs {
        a300keys
        obc5keys
    }
    compileSdkVersion 24
    //buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.micronet.dsc.vbs"
        versionCode 302
        versionName "3.0.2"
	    setProperty("archivesBaseName", "MicronetVBS-v" + versionName )
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //signingConfig signingConfigs.a300keys
        }
        debug {
            //signingConfig signingConfigs.a300keys
	        versionNameSuffix "-debug"
            signingConfig null // trick to eliminate default signing config for debug so it can be specified in flavors
        }
    }

    flavorDimensions "device"
    productFlavors {
        a317 {
            dimension "device"
            applicationIdSuffix "" // leave this blank because it was originally deployed this way
            versionNameSuffix "-a317"
            buildConfigField "String", "BUILD_DEVICE", "\"a317\""
            buildConfigField "String", "ATS_ID_SUFFIX", "\"\""  // for talking to ATS
            minSdkVersion 15
            targetSdkVersion 15
            signingConfig signingConfigs.a300keys
        }
        obc5 {
            dimension "device"
            //applicationIdSuffix ".obc5"
            applicationIdSuffix "" // leave this blank because ATS uses the application name to start VBS
            versionNameSuffix "-obc5"
            buildConfigField "String", "BUILD_DEVICE", "\"obc5\""
            buildConfigField "String", "ATS_ID_SUFFIX", "\".obc5\"" // for talking to ATS
            minSdkVersion 22
            targetSdkVersion 22
            signingConfig signingConfigs.obc5keys
            dependencies {
                implementation fileTree(dir: 'obc5/jniLibs', include: ['.so'])
            }
        }
    }


}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    //compile fileTree(dir: 'libs', include: ['.so'])
    //implementation 'junit:junit:4.12'
    //compile 'com.android.support:appcompat-v7:21.0.3'
    implementation 'com.android.support:support-core-utils:24.2.0'
    a317Implementation files('libs/a317_canbus_api.jar')
    obc5Implementation files('libs/obc5_vehiclebus_api.jar')

}

def a300_props = new Properties()
a300_props.load(new FileInputStream(rootProject.file("a300keys.properties")))
android.signingConfigs.a300keys.storeFile rootProject.file(a300_props.keyStore)
android.signingConfigs.a300keys.storePassword a300_props.keyStorePassword
android.signingConfigs.a300keys.keyAlias a300_props.keyAlias
android.signingConfigs.a300keys.keyPassword a300_props.keyAliasPassword

def obc5_props = new Properties()
obc5_props.load(new FileInputStream(rootProject.file("obc5keys.properties")))
android.signingConfigs.obc5keys.storeFile rootProject.file(obc5_props.keyStore)
android.signingConfigs.obc5keys.storePassword obc5_props.keyStorePassword
android.signingConfigs.obc5keys.keyAlias obc5_props.keyAlias
android.signingConfigs.obc5keys.keyPassword obc5_props.keyAliasPassword